#===============================================================================
# Copyright 2019 Intel Corporation
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#===============================================================================

cmake_minimum_required(VERSION 2.8.11)
project (MKLDNN_EXAMPLES)

if(POLICY CMP0015)
    cmake_policy(SET CMP0015 NEW)
endif()

set(MKLDNNROOT "${PROJECT_SOURCE_DIR}/.." CACHE STRING "")

enable_testing()

if(WIN32)
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} /Qstd=c99")
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /Qstd=c++11")
else()
    set(LIBM m)
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -std=c99")
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11")
    if(NOT APPLE)
        set(CMAKE_EXE_LINKER_FLAGS "-Wl,--as-needed")
    endif()
endif()

if(WIN32 AND ${CMAKE_CXX_COMPILER_ID} STREQUAL MSVC)
    add_definitions(/Qpar)
    add_definitions(/openmp)
else()
    find_package(OpenMP)
    #newer version for findOpenMP (>= v. 3.9)
    if(CMAKE_VERSION VERSION_LESS "3.9" AND OPENMP_FOUND)
        if(${CMAKE_MAJOR_VERSION} VERSION_LESS "3" AND
                ${CMAKE_CXX_COMPILER_ID} STREQUAL "Intel")
            # Override FindOpenMP flags for Intel Compiler (otherwise deprecated)
            set(OpenMP_CXX_FLAGS "-fopenmp")
            set(OpenMP_C_FLAGS "-fopenmp")
        endif()
        set(OpenMP_C_FOUND true)
        set(OpenMP_CXX_FOUND true)
    endif()
    if(OpenMP_C_FOUND)
        set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${OpenMP_C_FLAGS}")
    endif()
    if(OpenMP_CXX_FOUND)
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}")
    endif()
endif()

# Register example
#   name -- name of the executable
#   srcs -- list of source, if many must be enclosed with ""
function(register_example name srcs)
    add_executable(${name} ${srcs})
    find_package(mkldnn @PROJECT_VERSION@ CONFIG REQUIRED HINTS ${MKLDNNROOT})
    target_link_libraries(${name} MKLDNN::mkldnn ${LIBM})
    add_test(${name} ${name})
    if(WIN32 OR MINGW)
        set_property(TEST ${name} PROPERTY ENVIRONMENT "PATH=${CTESTCONFIG_PATH};$ENV{PATH}")
        configure_file(template.vcxproj.user ${name}.vcxproj.user @ONLY)
    endif()
endfunction()

register_example(cpu-getting-started cpu_getting_started.cpp)
register_example(cpu-memory-format-propagation cpu_memory_format_propagation.cpp)

register_example(cpu-cnn-inference-fp32-c cpu_cnn_inference_fp32.c)
register_example(cpu-cnn-inference-fp32-cpp cpu_cnn_inference_fp32.cpp)
register_example(cpu-cnn-training-fp32-c cpu_cnn_training_fp32.c)
register_example(cpu-cnn-training-fp32-cpp cpu_cnn_training_fp32.cpp)
register_example(cpu-cnn-inference-int8-cpp cpu_cnn_inference_int8.cpp)
register_example(cpu-rnn-inference-fp32-cpp cpu_rnn_inference_fp32.cpp)
register_example(cpu-rnn-inference-int8 cpu_rnn_inference_int8.cpp)
register_example(cpu-rnn-training-fp32 cpu_rnn_training_fp32.cpp)
